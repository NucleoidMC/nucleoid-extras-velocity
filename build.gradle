plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'me.geek.tom'
version = '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    maven {
        name = 'velocitypowered-repo'
        url = 'https://repo.velocitypowered.com/releases/'
    }
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'tomthegeek-repo'
        url = 'https://maven.tomthegeek.ml/releases/'
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    implementation 'com.velocitypowered:velocity-api:1.1.0'
    annotationProcessor 'com.velocitypowered:velocity-api:1.1.0'
    shade 'io.netty:netty-all:4.1.63.Final'
    shade('com.github.tom_the_geek:nucleoid-api-client:1.1.0') {
        exclude module: 'slf4j-api'
    }
}

shadowJar {
    configurations = [project.configurations.shade]
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "me.geek.tom.nucleoidextrasvelocity.libs"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

import org.apache.tools.ant.filters.ReplaceTokens

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    inputs.property 'version', version
    filter ReplaceTokens, tokens: [version: version]
    into "$buildDir/src"
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

compileJava.source = processSources.outputs

build.dependsOn shadowJar
